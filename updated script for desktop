study(title="UT Bot Alerts", overlay=true)

// inputs
a = define(1.0, name="Key Value (Sensitivity)")
c = define(10, name="ATR Period")

// calculate ATR
xATR = ind.atr(c)
nLoss = a * xATR

// Source price
// TODO: use Signals from Heikin Ashi Candles
src = close

// compute ATR Trailing Stop
xATRTrailingStop = 0.0
xATRTrailingStop := iff((src > xATRTrailingStop[1]) and (src[1] > xATRTrailingStop[1]), math.max(xATRTrailingStop[1], src - nLoss),
    iff((src < xATRTrailingStop[1]) and (src[1] < xATRTrailingStop[1]),
        math.min(xATRTrailingStop[1], src + nLoss),
        iff(src > xATRTrailingStop[1], src - nLoss, src + nLoss)
    )
)

// position tracking
pos = 0
pos := iff(
    (src[1] < xATRTrailingStop[1]) and (src > xATRTrailingStop[1]),
    1,
    iff(
        (src[1] > xATRTrailingStop[1]) and (src < xATRTrailingStop[1]),
        -1,
        pos[1]
    )
)

// Exponential Moving Average
emaVal = ind.ema(src, 1)

// calculate crossover 
above = (emaVal[1] < xATRTrailingStop[1]) and (emaVal > xATRTrailingStop)
below = (emaVal[1] > xATRTrailingStop[1]) and (emaVal < xATRTrailingStop)

// calculate signals
buySignal = (src > xATRTrailingStop) and above
sellSignal = (src < xATRTrailingStop) and below

// plot the ATR Trailing Stop 
plotATRTrailingStop = plt(xATRTrailingStop, name="ATR Trailing Stop", color=color.gray, line_width=2)

// compute offset for signals
offset = ind.atr(14) * 0.2

// plot Buy Signals above the candle
buyPlot = iff(buySignal, high + offset, none)
plt(buyPlot, name="Buy Signal", color=color.green, type=plt.type_circles, line_width=2)

// plot Sell Signals below the candle
sellPlot = iff(sellSignal, low - offset, none)
plt(sellPlot,
    name="Sell Signal",
    color=color.red,
    type=plt.type_circles,
    line_width=2
)
